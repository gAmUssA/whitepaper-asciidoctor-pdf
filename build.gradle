plugins {
  id 'org.asciidoctor.jvm.convert' version '3.1.0'
  id 'org.asciidoctor.jvm.pdf' version '3.1.0'
}

repositories {
  jcenter()
}

pdfThemes {
  ['custom'].each { tName ->
    local tName, {
      themeDir = 'src/docs/asciidoc/theme'
    }
  }
}

tasks.withType(AsciidoctorPdfTask).configureEach { adocPdfTask ->
  adocPdfTask.outputDir "${buildDir}/docs"
  adocPdfTask.attributes 'build-gradle': file('build.gradle'),
                         'endpoint-url': 'https://confluent.io',
                         'source-highlighter': 'coderay',
                         'icons': 'font',
                         'idprefix': '',
                         'idseparator': '-'
  adocPdfTask.fontsDir 'src/docs/asciidoc/theme/fonts'
  adocPdfTask.theme 'custom'
}

// task for generating specified White Paper (PDF only)
import org.asciidoctor.gradle.jvm.pdf.AsciidoctorPdfTask

tasks.create("buildWhitepaper", AsciidoctorPdfTask) {
  description "Generate specified White Paper"
  group "Whitepapers"

  def whitePaperName

  doFirst {
    if (project.hasProperty("h")) {
      def h = """
===================================================
Task Args:
whitepaper    Report to build. EX `whitepaper_filename`
e.g.

./gradlew buildWhitePaper -Pwhitepaper=Confluent_Platform_Reference_Architecture_for_Kubernetes
===================================================
"""
      println h
      throw new IllegalArgumentException("Help")
    }
    if (project.hasProperty("whitepaper")) {
      sources {
        whitePaperName = project.property("whitepaper")
        include "whitepapers/" + whitePaperName + ".adoc"
      }
    } else {
      throw new IllegalArgumentException("Missing a whitepaper name!")
    }
  }

  doLast {
    println "Finished paper (cmd+click): ${outputDir}/pdf/whitepapers/${whitePaperName}.pdf"
  }
}